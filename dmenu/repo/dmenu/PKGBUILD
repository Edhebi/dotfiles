pkgname=dmenu-local
_pkgsourcename=dmenu
pkgver=4.9
pkgrel=1
pkgdesc='A simple dynamic menu for X.'
arch=('x86_64')
license=('MIT')
provides=('dmenu')
conflicts=('dmenu')
depends=('sh' 'libxinerama' 'libxft' 'freetype2')
url="https://tools.suckless.org/dmenu"

_patches=()

source=("http://dl.suckless.org/tools/dmenu-$pkgver.tar.gz"
        "config.h"
        "${_patches[@]}")

sha256sums=('b3971f4f354476a37b2afb498693649009b201550b0c7c88e866af8132b64945'
            'b861525bda9f9f13a5faef0f075e1df2385e44d960e3360103394ecf12e65015')

prepare() {
  cd $srcdir/$_pkgsourcename-$pkgver

  for patch in "${_patches[@]}"; do
    patch -Np1 -i "$srcdir/$(basename $patch)"
  done
  cp $srcdir/config.h config.h

  echo "\nCPPFLAGS+=$CPPFLAGS\nCFLAGS+=$CFLAGS\nLDFLAGS+=$LDFLAGS" >> config.mk
}

build() {
  cd $srcdir/$_pkgsourcename-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd $srcdir/$_pkgsourcename-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/dmenu/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/dmenu/README"
}
