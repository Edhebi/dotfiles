pkgname=dmenu
pkgver=4.8
pkgrel=1
pkgdesc='A simple dynamic menu for X.'
arch=('x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft' 'freetype2')
url="https://tools.suckless.org/dmenu"

_patches=()

source=("http://dl.suckless.org/tools/dmenu-$pkgver.tar.gz"
        "config.h"
        "${_patches[@]}")

sha256sums=('fe615a5c3607061e2106700862e82ac62a9fa1e6a7ac3d616a9c76106476db61'
            'b08ebd39f5b9be1b5e2efedbb27dd1bc3e03e31903f415c5395ed88670180562')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  for patch in "${_patches[@]}"; do
    patch -Np1 -i "$srcdir/$(basename $patch)"
  done
  cp $srcdir/config.h config.h

  echo "\nCPPFLAGS+=$CPPFLAGS\nCFLAGS+=$CFLAGS\nLDFLAGS+=$LDFLAGS" >> config.mk
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
