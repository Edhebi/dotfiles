pkgname=dwm
pkgver=6.2
pkgrel=1
pkgdesc='A dynamic window manager for X'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11' 'libxinerama')
makedepends=('ncurses')
url="http://dwm.suckless.org"

_filedeps=('dwm.desktop' 'azer.h')
_patches=("https://dwm.suckless.org/patches/uselessgap/dwm-uselessgap-6.1.diff"
          "https://dwm.suckless.org/patches/restartsig/dwm-restartsig-20180523-6.2.diff"
		  'dwm-6.2-urg-border.diff'
		  )

source=("http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz"
        "config.h"
        "${_patches[@]}" "${_filedeps[@]}")

sha256sums=('97902e2e007aaeaa3c6e3bed1f81785b817b7413947f1db1d3b62b8da4cd110e'
            '38b8e4dc8d730af04e3a8159195ec884dc11f1bce3830027c7c554c5e8c16994'
            '502d3679bcb55109d43eaec5b62bcf1b20bdf4a7d0cb68018e9a4e5dfd568796'
            'c74f2f41da552b0c8629ddb9fec7b40a035581041cf198d81a43d4b16b742f69'
            '99fd919a2b30ae06c30c332d8b01cd9f48876f90b1be48c2f74a74189d4f2018'
            'e91732857fdf93ead79147ad4c51e4f98eebe9403dafe0d5e74ceb7f46891e81'
            '8adcea0853fad28a52588e1fd3e2c0150e86bfce91406065411c3cde1cc7006d')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  for patch in "${_patches[@]}"; do
    patch -Np1 -i "$srcdir/$(basename $patch)"
  done

  cp $srcdir/azer.h   azer.h
  cp $srcdir/config.h config.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -ir 's/FLAGS *=/FLAGS +=/' config.mk
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 README $pkgdir/usr/share/doc/$pkgname/README
  install -Dm644 $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}
