pkgname=surf
pkgver=2.0
pkgrel=2
pkgdesc='A simble web browser based on WebKit2/GTK+'
arch=('i686' 'x86_64')
license=('MIT')
depends=('webkit2gtk' 'xorg-xprop')
makedepends=('git')
url="http://surf.suckless.org"

_patches=("https://surf.suckless.org/patches/searchengines/surf-git-20170323-webkit2-searchengines.diff")

source=("https://dl.suckless.org/surf/surf-2.0.tar.gz"
        "config.h"
        "${_patches[@]}")

sha256sums=('faee4c7a62c38fc9791eff1ad06787c3c9b2b79f338806827f5152a7bc54951d'
            '4558a2daba94f5b3061b09752d4b0d07b35899eef3d0d47fe1aa83b9fcb1a236'
            'b5062e724f9eb7244e312f4d7b37fe84dd2869c6b154d199cbf51dd7710e0577')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  for patch in "${_patches[@]}"; do
    patch -Np1 -i "$srcdir/$(basename $patch)"
  done

  cp $srcdir/config.h config.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's/FLAGS =/FLAGS +=/g;' config.mk
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
